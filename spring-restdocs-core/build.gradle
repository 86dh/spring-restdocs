plugins {
	id "java-library"
	id "matrixtest"
	id "maven-publish"
	id "optional-dependencies"
}

description = "Spring REST Docs Core"

configurations {
	jarjar
	jmustache
	testArtifacts.extendsFrom testRuntime
}

task jmustacheRepackJar(type: Jar) { repackJar ->
	repackJar.archiveBaseName = "restdocs-jmustache-repack"
	repackJar.archiveVersion = jmustacheVersion

	doLast() {
		project.ant {
			taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask",
				classpath: configurations.jarjar.asPath
			jarjar(destfile: repackJar.archivePath) {
				configurations.jmustache.each { originalJar ->
					zipfileset(src: originalJar, includes: "**/*.class")
				}
				rule(pattern: "com.samskivert.**", result: "org.springframework.restdocs.@1")
			}
		}
	}
}

dependencies {
	implementation("com.fasterxml.jackson.core:jackson-databind")
	implementation("org.springframework:spring-web")
	implementation(files(jmustacheRepackJar))

	internal(platform(project(":spring-restdocs-platform")))

	jarjar("com.googlecode.jarjar:jarjar:1.3")

	jmustache(platform(project(":spring-restdocs-platform")))
	jmustache("com.samskivert:jmustache@jar")

	optional("commons-codec:commons-codec")
	optional("javax.validation:validation-api")
	optional("junit:junit")
	optional("org.hibernate.validator:hibernate-validator")
	optional("org.junit.jupiter:junit-jupiter-api")

	testImplementation("junit:junit")
	testImplementation("org.assertj:assertj-core")
	testImplementation("org.javamoney:moneta")
	testImplementation("org.mockito:mockito-core")
	testImplementation("org.hamcrest:hamcrest-core")
	testImplementation("org.hamcrest:hamcrest-library")
	testImplementation("org.springframework:spring-test")

	testRuntimeOnly("org.glassfish:javax.el:3.0.0")
}

jar {
	dependsOn jmustacheRepackJar
	from(zipTree(jmustacheRepackJar.archivePath)) {
		include "org/springframework/restdocs/**"
	}
}

task testJar(type: Jar) {
	classifier "test"
	from sourceSets.test.output
}

artifacts {
	testArtifacts testJar
}

matrixTest {
	springFramework {
		group = "org.springframework"
		versions = ["5.1.+", "5.2.+"]
	}
}
